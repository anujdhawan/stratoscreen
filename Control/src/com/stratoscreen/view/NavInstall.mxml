<?xml version="1.0" encoding="utf-8"?>
<s:NavigatorContent xmlns:fx="http://ns.adobe.com/mxml/2009" xmlns:s="library://ns.adobe.com/flex/spark" 
					xmlns:mx="library://ns.adobe.com/flex/mx" width="100%" height="100%" maxWidth="800" 
					creationComplete="creationCompleteHandler(event)" show="showHandler(event)" >
	<fx:Script>
		<![CDATA[
			import com.stratoscreen.aws.*
			import com.stratoscreen.managers.AppManager;
			import com.stratoscreen.model.SharedKeys;
			import com.stratoscreen.utils.SharedUtils;
			
			import mx.controls.Alert;
			import mx.events.CloseEvent;
			import mx.events.FlexEvent;
			import mx.managers.PopUpManager;
			
			private var _controlPanel:ControlPanel;
			private var _appManager:AppManager;
			private var _winProcess:WinProcess;
			private var _loaded:Boolean = false;
			private var _validCF:Boolean = false;
			private var _validS3:Boolean = false;
			private var _validSDB:Boolean = false;
			private var _validSES:Boolean = false;
			
			protected function creationCompleteHandler(event:FlexEvent):void
			{
				_controlPanel =  this.parentApplication as ControlPanel;
				_appManager = _controlPanel.appManager;				
			}			
			
			protected function showHandler(event:FlexEvent):void
			{
				if (!_loaded)
				{
					_appManager.cf.listDistributionsList(awsHandler);
					_appManager.s3.listAllBuckets(awsHandler);
					_appManager.sdb.listDomains(awsHandler);
					_appManager.ses.getQuota(awsHandler);
					
					// Set the selected region. The ID should start at one
					ddRegions.selectedIndex = _controlPanel.appManager.regionId - 1;

					_loaded = true;
				}				
			}
			
			protected function btnInstall_clickHandler(event:MouseEvent):void
			{
				SharedUtils.setValue(SharedKeys.REGION_ID_KEY, ddRegions.selectedItem.id.toString());
				_winProcess = new WinProcess();
				_winProcess.action = WinProcess.INSTALL;
				_winProcess.addEventListener(CloseEvent.CLOSE, processCloseHandler);
				PopUpManager.addPopUp(_winProcess, this.parentApplication as DisplayObject, true);
			}
			
			private function processCloseHandler(event:CloseEvent):void
			{
				
				if (_winProcess.action == WinProcess.INSTALL)
				{
					if (_winProcess.success)
					{
						Alert.show("Stratoscreen main service successfully installed", "Installation");
						_controlPanel.softwareInstalled = true;
						_controlPanel.appManager.accountBucket = _winProcess.webBucket;
	
						NavHome(this.parentDocument).vsMain.selectedChild =	NavHome(this.parentDocument).navOverview;
						NavHome(this.parentDocument).navOverview.updateFields();
					}
					else
					{
						var msg:String = "Stratoscreen main service installed failed.\r\n";
						msg += "Please run the uninstall option and try again.";
						
						Alert.show(msg, "Installation");					
					}
				}
				else
				{
					if (_winProcess.success)
					{
						Alert.show("Application successfully uninstalled", "Uninstall", 4, null, closeApplication );
					}
					else
					{
						msg = "Application did not uninstall properly.\r\n";
						msg += "Some AWS services may need to be manually removed.";						
						Alert.show(msg, "Uninstall", 4, null, closeApplication );		
					}					
					
					// We are done. Force the client to get out.
					SharedUtils.setValue(SharedKeys.ENCRYPTED_ACCESS_KEY, null);
					SharedUtils.setValue(SharedKeys.ENCRYPTED_SECRET_KEY, null);
					SharedUtils.setValue(SharedKeys.REGION_ID_KEY, null);									
				}
			}
			
			private function awsHandler(event:Object):void
			{
				var label:Label;
				if (event is S3Event)
				{
					label = lblS3;	
					_validS3 = event.success;
				}
				else if (event is SDBEvent)
				{
					label = lblSDB;
					_validSDB = event.success;
				}
				else if (event is SESEvent)
				{
					label = lblSES;
					_validSES = event.success;
				}
				else if (event is CFEvent)
				{
					label = lblCF;
					_validCF = event.success;
				}
				
				if (event.success)
				{
					label.text = "âˆš";
					label.setStyle("color","#008000");
				}
				else
				{
					label.text = "X";
					label.setStyle("color","#FF00000");					
				}
			}


			protected function btnUninstall_clickHandler(event:MouseEvent):void
			{
				_winProcess = new WinProcess();
				_winProcess.action = WinProcess.UNINSTALL;
				_winProcess.addEventListener(CloseEvent.CLOSE, processCloseHandler);
				PopUpManager.addPopUp(_winProcess, this.parentApplication as DisplayObject, true);
			}

			private function closeApplication(event:CloseEvent):void
			{
				NativeApplication.nativeApplication.exit();
			}			

		]]>
	</fx:Script>
	<s:Group width="100%" height="100%" maxWidth="800">
		<s:layout>
			<s:VerticalLayout/>
		</s:layout>
		<s:TextArea heightInLines="NaN" width="100%" styleName="Header">
			<s:text>
			Configure your Amazon Web Services subscription to run the StratoScreen software.
			</s:text>
		</s:TextArea>
		<s:Group height="260">
			<s:Label x="29" y="28" text="?" fontWeight="bold" color="#808080" id="lblCF"/>
			<s:Label x="48" y="28" text="Cloudfront Service"/>
			<s:Label x="29" y="46" text="?" fontWeight="bold" color="#808080" id="lblSES"/>
			<s:Label x="48" y="46" text="Simple Email Service"/>
			<s:Label x="29" y="64" text="?" fontWeight="bold" color="#808080" id="lblSDB"/>
			<s:Label x="48" y="64" text="Simple Database Service"/>
			<s:Label x="29" y="82" text="?" fontWeight="bold" color="#808080" id="lblS3"/>
			<s:Label x="48" y="82" text="Simple Storage Service"/>
			<s:Label x="11" y="112" text="Region" fontWeight="bold"/>
			<s:DropDownList x="30" y="134" id="ddRegions" 
							dataProvider="{AWSRegions.endpoints}" labelField="name" width="225"/>
			<s:Button x="29" y="188" label="Install" id="btnInstall" click="btnInstall_clickHandler(event)"/>
			<s:Label x="113" y="224" text="Rerun the uninstall process to clean a prior installation."  fontStyle="italic" color="#808080"/>
			<s:Button x="29" y="217" label="Uninstall" id="btnUninstall" click="btnUninstall_clickHandler(event)"/>
			<s:Label x="10" y="10" text="Amazon Web Services" fontWeight="bold"/>
			<s:Label text="Select the closest AWS region for your installations"  fontStyle="italic" x="263" y="138" color="#808080"/>
		</s:Group>
	</s:Group>		
</s:NavigatorContent>
