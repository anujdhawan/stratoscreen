<?xml version="1.0" encoding="utf-8"?>
<s:TitleWindow xmlns:fx="http://ns.adobe.com/mxml/2009"  xmlns:s="library://ns.adobe.com/flex/spark" 
			   xmlns:mx="library://ns.adobe.com/flex/mx" width="370" height="210" title="Change password" 
			   creationComplete="creationCompleteHandler(event)" close="closeHandler(event)" >
	<fx:Declarations>
		<mx:StringValidator id="validPassword" source="{txtPassword}" property="text" minLength="{Constants.MINIMUM_PASSWORD_LENGTH}" />		
	</fx:Declarations>
	<fx:Script>
		<![CDATA[
			import com.stratoscreen.Constants;
			import com.stratoscreen.aws.SDBEvent;
			import com.stratoscreen.managers.AppManager;
			import com.stratoscreen.model.domains.Users;
			
			import mx.controls.Alert;
			import mx.events.CloseEvent;
			import mx.events.FlexEvent;
			import mx.events.ValidationResultEvent;
			import mx.managers.PopUpManager;
			import mx.utils.StringUtil;

			private var _appManager:AppManager;
			private var _user:Users;			
			private var _canceled:Boolean = true;	

			protected function creationCompleteHandler(event:FlexEvent):void
			{
				PopUpManager.centerPopUp(this);
				_appManager = this.parentApplication.appManager;
				_user = _appManager.currentUser;				 
			}

			public function get canceled():Boolean
			{
				return _canceled;
			}

			protected function closeHandler(event:CloseEvent):void
			{
				if (event == null) {this.dispatchEvent(new CloseEvent(CloseEvent.CLOSE));}
				PopUpManager.removePopUp(this);
			}


			protected function btnSave_clickHandler(event:MouseEvent):void
			{
				if (DemoUtils.isDemo()) {return;}
				
				txtCurrentPassword.errorString = "";
				txtPassword.errorString = "";
				txtPassword2.errorString = "";
				lblStatus.text = "";
				
				txtCurrentPassword.text = StringUtil.trim(txtCurrentPassword.text);
				txtPassword.text = StringUtil.trim(txtPassword.text);
				txtPassword2.text = StringUtil.trim(txtPassword2.text);
				
				var result:ValidationResultEvent = validPassword.validate();
				if (result.results != null)  
				{
					lblStatus.text = "New password is too short";
					return;
				}
				
				if (txtCurrentPassword.text != _user.decryptedPassword)
				{
					txtCurrentPassword.errorString = "Password does not match current password";
					lblStatus.text = txtCurrentPassword.errorString;
					return;
				}
				
				if (txtPassword.text != txtPassword2.text)
				{
					txtPassword.errorString = "New passwords do not match";
					txtPassword2.errorString = txtPassword.errorString;
					lblStatus.text = txtPassword.errorString;
					return;
				}

				if (txtCurrentPassword.text == txtPassword.text)
				{
					txtCurrentPassword.errorString = "Please enter a new password";
					txtPassword.errorString = txtPassword.errorString;
					lblStatus.text = txtPassword.errorString;
					return;
				}

				
				// Update the database
				_user.decryptedPassword = txtPassword.text;
				_user.updated = true;
				_user.changePassword = "0";	// Update any forced flags too
				_appManager.sdb.updateDomain([_user], userUpdateHandler);
			}
			
			private function userUpdateHandler(event:SDBEvent):void
			{
				if (!event.success)
				{
					Alert.show("The new password could not be saved to the cloud. Please try again later", "Update Error");
					return;
				}
				
				_canceled = false;
				closeHandler(null);;
			}

		]]>
	</fx:Script>
	<s:controlBarContent>
		<s:Label id="lblStatus" />	
	</s:controlBarContent>
	<s:Label x="10" y="10" text="Current password" height="20"/>
	<s:TextInput y="10" width="211" id="txtCurrentPassword" right="10" displayAsPassword="true" enter="btnSave_clickHandler(null)"/>
	<s:Label x="10" y="41" text="New password" height="20"/>
	<s:TextInput y="41" width="211" id="txtPassword" right="10" displayAsPassword="true" enter="btnSave_clickHandler(null)"/>
	<s:Label x="10" y="71" text="Re-enter new password" height="20" fontStyle="italic"/>
	<s:TextInput y="71" width="211" id="txtPassword2" right="10" displayAsPassword="true" enter="btnSave_clickHandler(null)"/>
	<s:Button x="288" y="122" label="Save" id="btnSave" click="btnSave_clickHandler(event)"/>
</s:TitleWindow>
