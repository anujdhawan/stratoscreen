<?xml version="1.0" encoding="utf-8"?>
<s:TitleWindow xmlns:fx="http://ns.adobe.com/mxml/2009" xmlns:s="library://ns.adobe.com/flex/spark" 
			   xmlns:mx="library://ns.adobe.com/flex/mx" xmlns:components="com.stratoscreen.components.*" 
			   minWidth="600" minHeight="300" title="Select Media Group"
			    creationComplete="this_creationCompleteHandler(event)" close="this_closeHandler(event)" >
	<fx:Script>
		<![CDATA[
			import com.stratoscreen.Constants;
			import com.stratoscreen.aws.SDBEvent;
			import com.stratoscreen.managers.AppManager;
			import com.stratoscreen.model.domains.MediaGroups;
			import com.stratoscreen.renderers.MediaGroupItem;
			
			import mx.collections.ArrayCollection;
			import mx.collections.Sort;
			import mx.collections.SortField;
			import mx.controls.Alert;
			import mx.events.CloseEvent;
			import mx.events.FlexEvent;
			import mx.managers.PopUpManager;
			import mx.utils.StringUtil;
			
			[Bindable] private var _groups:ArrayCollection;
			private var _appManager:AppManager;
			private var _canceled:Boolean = true;
			private var _selectedGroup:MediaGroups = null;
			
			public function get canceled():Boolean
			{
				return _canceled;
			}
			
			public function get selectedMediaGroup():MediaGroups
			{
				return _selectedGroup;
			}

			protected function this_creationCompleteHandler(event:FlexEvent):void
			{
				PopUpManager.centerPopUp(this);
				
				_appManager = this.parentApplication.appManager;
				_appManager.currentEditWindow = this;	// Make sure this is set back after it is closed
				
				searchBox.addEventListener(SearchBox.SEARCH_SEARCH, searchHandler);
				searchBox.addEventListener(SearchBox.SEARCH_CLEARED, searchHandler);
				
				var sql:String = "Select * from MediaGroups where accountId='";
				sql += _appManager.currentUser.accountId + "'";
				_appManager.sdb.select(sql, loadGroupsHandler, MediaGroups);
				this.enabled = false;
			}
			
			public function itemClick(item:MediaGroupItem):void
			{
				_selectedGroup = item.data as MediaGroups;
				_canceled = false;
				this_closeHandler(null);
			}
			
			private function loadGroupsHandler(event:SDBEvent):void
			{
				this.enabled = true;
				
				if (event.success)
				{
					_groups = new ArrayCollection();
					_groups.source = event.result as Array;
					
					if (_groups.length == 0)
					{
						Alert.show("You do not have any Media Groups created.\r\n" +
							       "Please create a Media Group before creating a Media Group Overlay.", "No Media Groups");						
						this_closeHandler(null);
						return;
					}
	
					// Default to a sort by name
					var sort:Sort = new Sort();
					sort.fields = [new SortField("name", true)];				
					_groups.sort = sort;
					_groups.refresh();
				}
				else
				{
					Alert.show("Could not load Media Groups from server. Please try again later", "Server Error");
				}
			}
			
			protected function this_closeHandler(event:CloseEvent):void
			{
				if (event == null) {this.dispatchEvent(new CloseEvent(CloseEvent.CLOSE));}
				PopUpManager.removePopUp(this);
			}
			
			private function filterMediaGroups(item:Object):Boolean
			{
				if (item.deleted) {return false;} // Always hide the deleted itens
				if (!item.uploaded) {return false;} // Cannot work with files until they are uploaded
				if (item.mediaType == Constants.MEDIA_TYPE_AUDIO) {return false;}
				
				var mediaName:String = item.name.toLowerCase();
				var searchText:String = searchBox.txtSearch.text.toLowerCase();
				searchText = StringUtil.trim(searchText);
				
				return mediaName.indexOf(searchText) >= 0;
			}
			
			private function searchHandler(event:Event):void
			{
				_groups.refresh();
			}

		]]>
	</fx:Script>
	<s:List itemRenderer=" com.stratoscreen.renderers.MediaGroupItem" dataProvider="{_groups}" borderVisible="false" left="5" top="35" bottom="5" right="5">
		<s:layout>
			<s:TileLayout orientation="rows"/>
		</s:layout>			
	</s:List>
	<components:SearchBox id="searchBox" right="5" top="5" width="50%" />

	<mx:SWFLoader id="swfLoading" visible="{!this.enabled}" horizontalCenter="0" verticalCenter="0" width="100" height="100" 
				  source="@Embed('assets/swfs/spinner.swf')" autoLoad="true"/>

	<s:controlBarContent>	
	</s:controlBarContent>
</s:TitleWindow>
