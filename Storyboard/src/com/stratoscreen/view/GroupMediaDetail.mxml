<?xml version="1.0" encoding="utf-8"?>
<s:Group xmlns:fx="http://ns.adobe.com/mxml/2009" xmlns:s="library://ns.adobe.com/flex/spark" 
		 xmlns:mx="library://ns.adobe.com/flex/mx" width="100%" xmlns:view="com.stratoscreen.view.*" 
		 xmlns:components="com.stratoscreen.components.*" 		 
		 height="135" contentBackgroundAlpha="0.0" creationComplete="creationCompleteHandler(event)" >
	<fx:Script>
		<![CDATA[
			import com.stratoscreen.Constants;
			import com.stratoscreen.managers.AppManager;
			import com.stratoscreen.model.domains.Medias;
			import com.stratoscreen.utils.Utils;
			
			import mx.events.CloseEvent;
			import mx.events.FlexEvent;
			import mx.formatters.DateFormatter;
			import mx.managers.PopUpManager;

			public static const DETAIL_CLOSE:String = "DETAIL_CLOSE";
			public static const MEDIA_DELETE:String = "MEDIA_DELETE";
			public static const MEDIA_EDIT:String = "MEDIA_EDIT";
			public static const MEDIA_DATA_EDIT:String = "MEDIA_DATA_EDIT";

			[Bindable][Embed(source="/assets/icons/cross.png")]
			private var iconClose:Class;

			[Bindable] private var _media:Medias;
			[Bindable] private var _canEdit:Boolean = false;;
			private var _appManager:AppManager;
			private var _winEdit:WinMediaEdit;
			
			protected function creationCompleteHandler(event:FlexEvent):void
			{
				_appManager = this.parentApplication.appManager;
				this.media = _media; 				
			}
			
			public function get media():Medias
			{
				return _media;
			}			
				
			public function set media(value:Medias):void
			{
				_media = value;
				if (_media == null) {return;}
				if (groupMain == null) {return;}	//still renedering			
				
				previewPlayer.stop();	// It is possible that this was running from the last preview
				
				_canEdit = _appManager.currentUser.type == Constants.USER_TYPE_MANAGER || _media.createdBy == _appManager.currentUser.itemName;
				linkDelete.visible = _media.uploaded;
				var formatter:DateFormatter = new DateFormatter();
				formatter.formatString = "MMM D, YYYY";
				lblCreated.text = formatter.format(_media.createdDate);					
				lblModified.text = formatter.format(_media.modifiedDate);		
				lblDimensions.text = _media.mediaType == Constants.MEDIA_TYPE_AUDIO ? "" : _media.width  + " x " +  _media.height;
				lblSize.text = Utils.formatBytes(uint(_media.size));
				
				// Format the duration. If the duration plays to end check the calculated
				var calcUsed:Boolean = false;
				var duration:int = parseInt(_media.duration);
				if (duration == 0) 
				{
					calcUsed = true;
					duration = parseInt(_media.calcDuration);
				}
				lblDuration.text = duration <= 0 ?  "To end" : Utils.formatDuration(duration);
				
				if (duration > 0 && calcUsed)
				{
					lblDuration.text += "*";
					lblDuration.toolTip = "Calculated duration";
				}

				if (_media.mediaType == Constants.MEDIA_TYPE_SWF)
				{
					if (parseInt(_media.frames) > 0)  {lblDuration.text = _media.frames + " frames";}
				}


				// Find the user who owns this item
				for (var i:int = 0; i < _appManager.usersHdrs.length; i++)
				{
					if (_appManager.usersHdrs[i].itemName == _media.createdBy)
					{
						lblCreatedBy.text = _appManager.usersHdrs[i].firstName + " " + 
							                _appManager.usersHdrs[i].lastName;
					}

					if (_appManager.usersHdrs[i].itemName == _media.modifiedBy)
					{
						lblModifiedBy.text = _appManager.usersHdrs[i].firstName + " " + 
							_appManager.usersHdrs[i].lastName;
					}
				}
				
				
			}
			
			public function editMedia():void
			{
				linkEdit_clickHandler(null);
			}

			protected function btnClose_clickHandler(event:MouseEvent):void
			{
				this.dispatchEvent(new Event(DETAIL_CLOSE));
			}


			protected function linkDelete_clickHandler(event:MouseEvent):void
			{
				this.dispatchEvent(new Event(MEDIA_DELETE));
			}


			protected function linkEdit_clickHandler(event:MouseEvent):void
			{
				previewPlayer.stop();
				_winEdit = new WinMediaEdit();
				_winEdit.media = _media;
				_winEdit.addEventListener(CloseEvent.CLOSE, editCloseHandler);
				PopUpManager.addPopUp(_winEdit, this.parentApplication as DisplayObject, true);
			}

			private function editCloseHandler(event:CloseEvent):void
			{
				if (_winEdit.canceled) {return;}
				
				_media = _winEdit.media;
				if (_media.updated) 
				{
					this.dispatchEvent(new Event(MEDIA_EDIT));
				}	
				else
				{
					this.dispatchEvent(new Event(MEDIA_DATA_EDIT));
				}
			}
		]]>
	</fx:Script>
	<s:BorderContainer styleName="DetailBand" left="1" top="1" right="1" bottom="1" >
		<s:HGroup id="groupMain" width="100%" height="100%" verticalAlign="middle">
			<mx:Spacer width="0" />
			<components:MediaPlayer id="previewPlayer" height="{this.height-6}" width="150" media="{_media}" viewMode="L" />
			<s:Line rotation="90" height="90%" >
				<s:stroke>
					<s:SolidColorStroke color="#3E63C1" weight="1" />
				</s:stroke>				
			</s:Line>
			<s:Group height="100%" >
				<s:Label x="0" y="10" text="Name" fontWeight="bold"/>
				<s:Label x="75" y="10" text="{_media.name}" maxDisplayedLines="1" maxWidth="300"/>
				<s:Label x="0" y="32" text="Dimensions" fontWeight="bold"/>
				<s:Label x="75" y="32" text=" " id="lblDimensions"/>				
				<s:Label x="0" y="54" text="Size" fontWeight="bold"/>
				<s:Label x="75" y="54" text=" " id="lblSize"/>
				<s:Label x="0" y="76" text="Duration" fontWeight="bold"/>
				<s:Label x="75" y="76" text=" " id="lblDuration"/>				
				<s:Label x="0" y="98" text="Streaming" fontWeight="bold" visible="{_media.canStream == '1'}"/>
				<s:Label x="75" y="98" id="lblStream" text="{_media.canStream == '1' ? 'Enabled' : 'Disabled'}"  visible="{_media.canStream == '1'}" />
			</s:Group>
			<mx:Spacer width="10" />
			<s:Group height="100%" >
				<s:Label x="0" y="10" text="Created" fontWeight="bold"/>
				<s:Label x="75" y="10" text=" " id="lblCreated"/>
				<s:Label x="0" y="32" text="Created by" fontWeight="bold"/>
				<s:Label x="75" y="32" text=" " id="lblCreatedBy"/>
				<s:Label x="0" y="54" text="Modified" fontWeight="bold"/>
				<s:Label x="75" y="54" text=" " id="lblModified"/>
				<s:Label x="0" y="76" text="Modified by" fontWeight="bold"/>
				<s:Label x="75" y="76" text=" " id="lblModifiedBy"/>
			</s:Group>		
			<mx:Spacer width="100%" />
			<s:VGroup height="100%"  paddingBottom="2" paddingLeft="2" paddingRight="2" paddingTop="2" textAlign="right">				
				<s:HGroup width="100%">
					<mx:Spacer width="100%" />
					<components:IconButton id="btnClose" click="btnClose_clickHandler(event)" icon="{iconClose}" />
				</s:HGroup>
				<mx:Spacer height="100%" />
				<components:IconButton label="Edit Media" id="linkEdit" icon="@Embed(source='/assets/icons/pencil.png')"  
									width="{linkDelete.width}"  click="linkEdit_clickHandler(event)" enabled="{_canEdit}" />
				<components:IconButton  label="Delete Media" id="linkDelete" icon="@Embed(source='/assets/icons/delete.png')"  
										 click="linkDelete_clickHandler(event)" enabled="{_canEdit}" isDelete="true"/>
			</s:VGroup>
		</s:HGroup>
	</s:BorderContainer>
</s:Group>
